*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        assign_remove_floating_ip
Default Tags
Test Template     Assign-Remove Floating IP Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ComputeURL              TokenID                TenantID                ServerID                Operation                   IP
Asssociate Floating IP to Server
                      [Tags]    main_scenarios
                      OK        ${OS_Compute_URL}       blank                  blank                   blank                   Associate Floating IP       ${Floating_IP}

Disassociate Floating IP from Server
                      [Tags]    main_scenarios
                      OK        ${OS_Compute_URL}       blank                  blank                   blank                   Disassociate Floating IP    ${Floating_IP}

Assertion with Invalid Compute URL
                      NOK       https://162.123.12.3    blank                  blank                   blank                   Associate Floating IP       ${Floating_IP}

Assertion with Invalid Authentication Token
                      NOK       ${OS_Compute_URL}       ${Invalid_Token_ID}    blank                   blank                   Associate Floating IP       ${Floating_IP}

Assertion with Invalid Tenant ID
                      NOK       ${OS_Compute_URL}       blank                  ${Invalid_Tenant_ID}    blank                   Associate Floating IP       ${Floating_IP}

Assertion with Invalid Server ID
                      NOK       ${OS_Compute_URL}       blank                  blank                   ${Invalid_Server_ID}    Associate Floating IP       ${Floating_IP}

Assertion with Invalid Floating IP
                      NOK       ${OS_Compute_URL}       blank                  blank                   blank                   Associate Floating IP       11.22.33.44.55

Assert Compute URL as a mandatory field
                      OTHER     ${EMPTY}                blank                  blank                   ${Invalid_Server_ID}    Associate Floating IP       ${Floating_IP}

Assert Authentication Token as a mandatory field
                      OTHER     ${OS_Compute_URL}       ${EMPTY}               blank                   ${Invalid_Server_ID}    Associate Floating IP       ${Floating_IP}

Assert Tenant ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  ${EMPTY}                ${Invalid_Server_ID}    Associate Floating IP       ${Floating_IP}

Assert Server ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   ${EMPTY}                Associate Floating IP       ${Floating_IP}

Assert Operation as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   blank                   ${EMPTY}                    ${Floating_IP}

Assert Floating IP as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   blank                   Disassociate Floating IP    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "ASSIGN/REMOVE FLOATING IP"*******
    Log    *******Connect to AE*******
    Connect AE

Assign-Remove Floating IP Template
    [Arguments]    ${OK_NOK}    ${Compute_URL}    ${Token_ID}    ${Tenant_ID}    ${Server_ID}    ${Operation}
    ...    ${IP}
    [Documentation]    This test creates the server snapshot when server state is active, shutoff, suspended or resume.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Compute_URL}: This field specifies OpenStack Compute URL.
    ...    -${Token_ID}: This field specifies Token ID generated as the output of Get Auth Token action.
    ...    -${Tenant_ID}: This field specifies Tenant ID generated as the output of Get Auth Token action.
    ...    -${Server_ID}: This field specifies Server ID whose snapshot is to be created.
    ...    -${Operation}: This field specifies Allocate or Deallocate Floating IP operation with Server.
    ...    -${IP}: This field specifies the IP for the servers.
    Run Keyword If    '${Generated_Token_ID}' == '${EMPTY}'    Get Auth Token    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    Run Keyword If    '${Generated_Server_ID}' == '${EMPTY}'    Create Server    ${OS_Compute_URL}    ${Generated_Token_ID}    ${Generated_Tenant_ID}    ${Valid_Create_Server}
    Run Keyword If    '${Generated_Server_ID}' == '${EMPTY}'    Get Server ID    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    ...    ${OS_Compute_URL}    ${Query}
    Action Create    ${MANAGE_SERVER_FLOATING_IP_ACTION}
    Action Set    &UC4RB_OPS_COMPUTE_URL#    ${Compute_URL}
    Run Keyword If    '${Token_ID}' == 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Generated_Token_ID}
    Run Keyword If    '${Token_ID}' != 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Token_ID}
    Run Keyword If    '${Tenant_ID}' == 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Generated_Tenant_ID}
    Run Keyword If    '${Tenant_ID}' != 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Tenant_ID}
    Run Keyword If    '${Server_ID}' == 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Generated_Server_ID}
    Run Keyword If    '${Server_ID}' != 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Server_ID}
    Action Set    &UC4RB_OPS_IP_OPERATION#    ${Operation}
    Action Set    &UC4RB_OPS_SERVER_IP#    ${IP}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory

Teardown
    Log    *******End Test "ASSIGN/REMOVE FLOATING IP"*******
