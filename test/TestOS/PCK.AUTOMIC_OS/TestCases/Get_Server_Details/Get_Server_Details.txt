*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_server_details
Default Tags
Test Template     Get Server Details Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ComputeURL              TokenID                TenantID                ServerID
Get Server Details with Valid Inputs
                      [Tags]    main_scenarios
                      OK        ${OS_Compute_URL}       blank                  blank                   blank

Assertion with Invalid Compute URL
                      NOK       https://162.123.12.3    blank                  blank                   blank

Assertion with Invalid Token ID
                      NOK       ${OS_Compute_URL}       ${Invalid_Token_ID}    blank                   blank

Assertion with Invalid Tenant ID
                      NOK       ${OS_Compute_URL}       blank                  ${Invalid_Tenant_ID}    blank

Assertion with Invalid Server ID
                      NOK       ${OS_Compute_URL}       blank                  blank                   ${Invalid_Server_ID}

Assert Compute URL as a mandatory field
                      OTHER     ${EMPTY}                blank                  blank                   ${Invalid_Server_ID}

Assert Token ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       ${EMPTY}               blank                   ${Invalid_Server_ID}

Assert Tenant ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  ${EMPTY}                ${Invalid_Server_ID}

Assert Server ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "GET SERVER DETAILS"*******
    Log    *******Connect to AE*******
    Connect AE

Get Server Details Template
    [Arguments]    ${OK_NOK}    ${Compute_URL}    ${Token_ID}    ${Tenant_ID}    ${Server_ID}
    [Documentation]    This test generates a XML containing details of a server.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Compute_URL}: This field specifies OpenStack Compute URL.
    ...    -${Token_ID}: This field specifies Token ID generated as the output of Get Auth Token action.
    ...    -${Tenant_ID}: This field specifies Tenant ID generated as the output of Get Auth Token action.
    ...    -${Server_ID}: This field specifies Server ID whose details are to be fetched.
    Run Keyword If    '${Generated_Token_ID}' == '${EMPTY}'    Get Auth Token    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    Run Keyword If    '${Generated_Server_ID}' == '${EMPTY}'    Create Server    ${OS_Compute_URL}    ${Generated_Token_ID}    ${Generated_Tenant_ID}    ${Valid_Create_Server}
    Action Create    ${_GET_SERVER_DETAILS_ACTION}
    Action Set    &UC4RB_OPS_COMPUTE_URL#    ${Compute_URL}
    Run Keyword If    '${Token_ID}' == 'blank'    Action Set    &UC4RB_OPS_TOKEN_ID#    ${Generated_Token_ID}
    Run Keyword If    '${Token_ID}' != 'blank'    Action Set    &UC4RB_OPS_TOKEN_ID#    ${Token_ID}
    Run Keyword If    '${Tenant_ID}' == 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Generated_Tenant_ID}
    Run Keyword If    '${Tenant_ID}' != 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Tenant_ID}
    Run Keyword If    '${Server_ID}' == 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Generated_Server_ID}
    Run Keyword If    '${Server_ID}' != 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Server_ID}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable File Path Should Be    &UC4RB_OPS_SERVER_INFO_FILEPATH#    .xml    ELSE    Action Object Variable Should Not Be Found
    ...    &UC4RB_OPS_SERVER_INFO_FILEPATH#
    Run Keyword If    '${OK_NOK}' == 'OK'    Delete File

Teardown
    Log    *******End Test "GET SERVER DETAILS"*******
