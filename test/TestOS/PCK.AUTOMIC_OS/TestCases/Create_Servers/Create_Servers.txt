*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_servers
Default Tags
Test Template     Create Servers Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ComputeURL              TokenID                TenantID                ParameterFile
Create Server with Valid Parameter File
                      [Tags]    main_scenarios
                      OK        ${OS_Compute_URL}       blank                  blank                   ${Valid_Create_Server}

Assertion with Invalid Compute URL
                      NOK       https://162.123.12.3    blank                  blank                   ${Valid_Create_Server}

Assertion with Invalid Token ID
                      NOK       ${OS_Compute_URL}       ${Invalid_Token_ID}    blank                   ${Valid_Create_Server}

Assertion with Invalid Tenant ID
                      NOK       ${OS_Compute_URL}       blank                  ${Invalid_Tenant_ID}    ${Valid_Create_Server}

Assertion with Invalid Parameter File
                      NOK       ${OS_Compute_URL}       blank                  blank                   ${Invalid_Create_Server}

Assertion with Invalid Parameter File Path
                      NOK       ${OS_Compute_URL}       blank                  blank                   c://abcd

Assert Compute URL as a mandatory field
                      OTHER     ${EMPTY}                blank                  blank                   ${Valid_Create_Server}

Assert Token ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       ${EMPTY}               blank                   ${Valid_Create_Server}

Assert Tenant ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  ${EMPTY}                ${Valid_Create_Server}

Assert Parameter File as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "CREATE SERVERS"*******
    Log    *******Connect to AE*******
    Connect AE

Create Servers Template
    [Arguments]    ${OK_NOK}    ${Compute_URL}    ${Token_ID}    ${Tenant_ID}    ${Parameter_File}
    [Documentation]    This test will create a new server and will publish its ID in AE variables.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Compute_URL}: This field specifies OpenStack Compute URL.
    ...    -${Token_ID}: This field specifies Token ID generated as the output of Get Auth Token action.
    ...    -${Tenant_ID}: This field specifies Tenant ID generated as the output of Get Auth Token action.
    ...    -${Parameter_File}: This field specifies path of the parameter file which is used to create server.
    #    blank input means Token ID and Tenant ID will get values from the output of Get Auth Token.
    #    Empty input means that field is left empty.
    Run Keyword If    '${Generated_Token_ID}' == '${EMPTY}'    Get Auth Token    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    Action Create    ${_CREATE_SERVERS_ACTION}
    Action Set    &UC4RB_OPS_COMPUTE_URL#    ${Compute_URL}
    Run Keyword If    '${Token_ID}' == 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Generated_Token_ID}
    Run Keyword If    '${Token_ID}' != 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Token_ID}
    Run Keyword If    '${Tenant_ID}' == 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Generated_Tenant_ID}
    Run Keyword If    '${Tenant_ID}' != 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Tenant_ID}
    Action Set    &UC4RB_OPS_PARAMETER_FILE#    ${Parameter_File}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_OPS_VARA_NAME#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_OPS_VARA_NAME#
    Run Keyword If    '${OK_NOK}' == 'OK'    Get Server ID    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    ...    ${OS_Compute_URL}    ${Query}

Teardown
    Delete Server    ${OS_Compute_URL}    ${Generated_Token_ID}    ${Generated_Tenant_ID}    ${Generated_Server_ID}
    Log    *******End Test "CREATE SERVERS"*******
