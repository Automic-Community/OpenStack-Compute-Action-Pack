*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_instance_snapshot
Default Tags
Test Template     Create Server Snapshot Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ComputeURL              TokenID                TenantID                ServerID                ImageName
Create Server Snapshot with Valid Inputs
                      [Tags]    main_scenarios
                      OK        ${OS_Compute_URL}       blank                  blank                   blank                   blank

Assertion with Invalid Compute URL
                      NOK       https://162.123.12.3    blank                  blank                   blank                   blank

Assertion with Invalid Authentication Token
                      NOK       ${OS_Compute_URL}       ${Invalid_Token_ID}    blank                   blank                   blank

Assertion with Invalid Tenant ID
                      NOK       ${OS_Compute_URL}       blank                  ${Invalid_Tenant_ID}    blank                   blank

Assertion with Invalid Server ID
                      NOK       ${OS_Compute_URL}       blank                  blank                   ${Invalid_Server_ID}    blank

Assert Compute URL as a mandatory field
                      OTHER     ${EMPTY}                blank                  blank                   ${Invalid_Server_ID}    blank

Assert Authentication Token as a mandatory field
                      OTHER     ${OS_Compute_URL}       ${EMPTY}               blank                   ${Invalid_Server_ID}    blank

Assert Tenant ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  ${EMPTY}                ${Invalid_Server_ID}    blank

Assert Image Name as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   blank                   ${EMPTY}

Assert Server ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   ${EMPTY}                blank

*** Keywords ***
Startup
    Log    *******Start Test "CREATE SERVER SNAPSHOT"*******
    Log    *******Connect to AE*******
    Connect AE

Create Server Snapshot Template
    [Arguments]    ${OK_NOK}    ${Compute_URL}    ${Token_ID}    ${Tenant_ID}    ${Server_ID}    ${Image_Name}
    [Documentation]    This test creates the server snapshot when server state is active, shutoff, suspended or resume.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Compute_URL}: This field specifies OpenStack Compute URL.
    ...    -${Token_ID}: This field specifies Token ID generated as the output of Get Auth Token action.
    ...    -${Tenant_ID}: This field specifies Tenant ID generated as the output of Get Auth Token action.
    ...    -${Server_ID}: This field specifies Server ID whose snapshot is to be created.
    ...    -${Image_Name}: This field specifies the name of snapshot created.
    #    blank input means Token ID and Tenant ID will get values from the output of Get Auth Token.
    #    Empty input means that field is left empty.
    #    blank input for server id will get server id from get server id template.
    Run Keyword If    '${Generated_Token_ID}' == '${EMPTY}'    Get Auth Token    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    Run Keyword If    '${Generated_Server_ID}' == '${EMPTY}'    Create Server    ${OS_Compute_URL}    ${Generated_Token_ID}    ${Generated_Tenant_ID}    ${Valid_Create_Server}
    Run Keyword If    '${Generated_Server_ID}' == '${EMPTY}'    Get Server ID    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    ...    ${OS_Compute_URL}    ${Query}
    Action Create    ${_CREATE_SERVER_SNAPSHOT_ACTION}
    Action Set    &UC4RB_OPS_COMPUTE_URL#    ${Compute_URL}
    Run Keyword If    '${Token_ID}' == 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Generated_Token_ID}
    Run Keyword If    '${Token_ID}' != 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Token_ID}
    Run Keyword If    '${Tenant_ID}' == 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Generated_Tenant_ID}
    Run Keyword If    '${Tenant_ID}' != 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Tenant_ID}
    Run Keyword If    '${Server_ID}' == 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Generated_Server_ID}
    Run Keyword If    '${Server_ID}' != 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Server_ID}
    Run Keyword If    '${Image_Name}' == 'blank'    Genarate String
    Run Keyword If    '${Image_Name}' == 'blank'    Action Set    &UC4RB_OPS_IMAGE_NAME#    ${Generated_Name}
    Run Keyword If    '${Image_Name}' != 'blank'    Action Set    &UC4RB_OPS_IMAGE_NAME#    ${EMPTY}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_OPS_IMAGE_URL#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_OPS_IMAGE_URL#

Teardown
    Delete Server    ${OS_Compute_URL}    ${Generated_Token_ID}    ${Generated_Tenant_ID}    ${Generated_Server_ID}
    Log    *******End Test "CREATE SERVER SNAPSHOT"*******
