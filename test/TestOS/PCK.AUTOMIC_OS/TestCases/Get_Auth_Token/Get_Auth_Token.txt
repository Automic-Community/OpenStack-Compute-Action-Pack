*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        login
Default Tags
Test Template     Get Auth Token Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    OpenStackURL            Username          Password          TenantName
Login with Valid Inputs
                      [Tags]    main_scenarios
                      OK        ${OS_Identity_URL}      ${UserName_OS}    ${Password_OS}    ${Name_Tenant}

Assertion with Invalid OS URL
                      NOK       https://162.123.12.3    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}

Assertion with Invalid OS Username
                      NOK       ${OS_Identity_URL}      testing           ${Password_OS}    ${Name_Tenant}

Assertion with Invalid OS Password
                      NOK       ${OS_Identity_URL}      ${UserName_OS}    testing           ${Name_Tenant}

Assertion with Invalid Tenant Name
                      NOK       ${OS_Identity_URL}      ${UserName_OS}    ${Password_OS}    testing

Assert Open Stack URL as a mandatory field
                      OTHER     ${EMPTY}                ${UserName_OS}    ${Password_OS}    ${Name_Tenant}

Assert Username as a mandatory field
                      OTHER     ${OS_Identity_URL}      ${EMPTY}          ${Password_OS}    ${Name_Tenant}

Assert Password as a mandatory field
                      OTHER     ${OS_Identity_URL}      ${UserName_OS}    ${EMPTY}          ${Name_Tenant}

Assert Tenant Name as a mandatory field
                      OTHER     ${OS_Identity_URL}      ${UserName_OS}    ${Password_OS}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "GET AUTH TOKEN"*******
    Log    *******Connect to AE*******
    Connect AE

Get Auth Token Template
    [Arguments]    ${OK_NOK}    ${Open_Stack_URL}    ${Username}    ${Password}    ${Tenant_Name}
    [Documentation]    This test generates a new token for openstack connection.
    ...    -Precondition:
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Open_Stack_URL}: This field specifies OpenStack API URL.
    ...    -${Username}: This field specifies Username for Open Stack.
    ...    -${Password}: This field specifies the password for Open Stack user.
    ...    -${Tenant_Name}: This field specifies the tenant name whose token is generated.
    Action Create    ${_GET_AUTH_TOKEN_ACTION}
    Get Auth Token    ${Open_Stack_URL}    ${Username}    ${Password}    ${Tenant_Name}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_OPS_LOGIN_INFO_KEY#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_OPS_LOGIN_INFO_KEY#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_OPS_LOGIN_SESSION_FILE#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_OPS_LOGIN_SESSION_FILE#

Teardown
    Log    *******End Test "GET AUTH TOKEN"*******
