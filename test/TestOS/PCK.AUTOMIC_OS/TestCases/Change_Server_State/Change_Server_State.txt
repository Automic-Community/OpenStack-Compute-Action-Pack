*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        change_server_state
Default Tags
Test Template     Change Server State Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ComputeURL              TokenID                TenantID                ServerID                Action
Stopping a Server     [Tags]    main_scenarios
                      OK        ${OS_Compute_URL}       blank                  blank                   blank                   STOP

Starting a Server     OK        ${OS_Compute_URL}       blank                  blank                   blank                   START

Pausing a Server      OK        ${OS_Compute_URL}       blank                  blank                   blank                   PAUSE

Unpausing a Server    OK        ${OS_Compute_URL}       blank                  blank                   blank                   UNPAUSE

Locking a Server      OK        ${OS_Compute_URL}       blank                  blank                   blank                   LOCK

Unlocking a Server    OK        ${OS_Compute_URL}       blank                  blank                   blank                   UNLOCK

Suspending a Server
                      OK        ${OS_Compute_URL}       blank                  blank                   blank                   SUSPEND

Resuming a Server     OK        ${OS_Compute_URL}       blank                  blank                   blank                   RESUME

Soft Rebooting a Server
                      OK        ${OS_Compute_URL}       blank                  blank                   blank                   REBOOT_SOFT

Hard Rebooting a Server
                      OK        ${OS_Compute_URL}       blank                  blank                   blank                   REBOOT_HARD

Assertion with Invalid Compute URL
                      NOK       https://162.123.12.3    blank                  blank                   blank                   START

Assertion with Invalid Authentication Token
                      NOK       ${OS_Compute_URL}       ${Invalid_Token_ID}    blank                   blank                   START

Assertion with Invalid Tenant ID
                      NOK       ${OS_Compute_URL}       blank                  ${Invalid_Tenant_ID}    blank                   START

Assertion with Invalid Server ID
                      NOK       ${OS_Compute_URL}       blank                  blank                   ${Invalid_Server_ID}    START

Assert Compute URL as a mandatory field
                      OTHER     ${EMPTY}                blank                  blank                   ${Invalid_Server_ID}    START

Assert Authentication Token as a mandatory field
                      OTHER     ${OS_Compute_URL}       ${EMPTY}               blank                   ${Invalid_Server_ID}    START

Assert Tenant ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  ${EMPTY}                ${Invalid_Server_ID}    START

Assert Server ID as a mandatory field
                      OTHER     ${OS_Compute_URL}       blank                  blank                   ${EMPTY}                START

*** Keywords ***
Startup
    Log    *******Start Test "CHANGE SERVER STATE"*******
    Log    *******Connect to AE*******
    Connect AE

Change Server State Template
    [Arguments]    ${OK_NOK}    ${Compute_URL}    ${Token_ID}    ${Tenant_ID}    ${Server_ID}    ${Action}
    [Documentation]    This test changes the state of a server.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Compute_URL}: This field specifies OpenStack Compute URL.
    ...    -${Token_ID}: This field specifies Token ID generated as the output of Get Auth Token action.
    ...    -${Tenant_ID}: This field specifies Tenant ID generated as the output of Get Auth Token action.
    ...    -${Server_ID}: This field specifies Server ID whose state is to be changed.
    ...    -${Action}: This field specifies the target action which user wants to perform on an instance.
    #    blank input means Token ID and Tenant ID will get values from the output of Get Auth Token.
    #    Empty input means that field is left empty.
    #    blank input for server id will get server id from get server id template.
    Run Keyword If    '${Generated_Token_ID}' == '${EMPTY}'    Get Auth Token    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    Run Keyword If    '${Generated_Server_ID}' == '${EMPTY}'    Create Server    ${OS_Compute_URL}    ${Generated_Token_ID}    ${Generated_Tenant_ID}    ${Valid_Create_Server}
    Run Keyword If    '${Generated_Server_ID}' == '${EMPTY}'    Get Server ID    ${OS_Identity_URL}    ${UserName_OS}    ${Password_OS}    ${Name_Tenant}
    ...    ${OS_Compute_URL}    ${Query}
    Action Create    ${_CHANGE_SERVER_STATE_ACTION}
    Action Set    &UC4RB_OPS_COMPUTE_URL#    ${Compute_URL}
    Run Keyword If    '${Token_ID}' == 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Generated_Token_ID}
    Run Keyword If    '${Token_ID}' != 'blank'    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Token_ID}
    Run Keyword If    '${Tenant_ID}' == 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Generated_Tenant_ID}
    Run Keyword If    '${Tenant_ID}' != 'blank'    Action Set    &UC4RB_OPS_TENANT_ID#    ${Tenant_ID}
    Run Keyword If    '${Server_ID}' == 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Generated_Server_ID}
    Run Keyword If    '${Server_ID}' != 'blank'    Action Set    &UC4RB_OPS_SERVER_ID#    ${Server_ID}
    Action Set    &UC4RB_OPS_SERVER_ACTION#    ${Action}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Sleep    60s

Teardown
    Delete Server    ${OS_Compute_URL}    ${Generated_Token_ID}    ${Generated_Tenant_ID}    ${Generated_Server_ID}
    Log    *******End Test "CHANGE SERVER STATE"*******
