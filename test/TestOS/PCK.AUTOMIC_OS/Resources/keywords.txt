*** Settings ***
Resource          variables.txt

*** Keywords ***
Connect AE
    Action Connect    ${_AE_HOST}    ${_AE_PORT}    ${_AE_USER}    ${_AE_PASS}    ${_AE_TEMP_FOLD}

Set Overwrite Agent
    Run Keyword If    '${ENV}' == 'WIN'    Set Overwrite Agent WIN
    Run Keyword If    '${ENV}' == 'UNIX'    Set Overwrite Agent UNIX
    Run Keyword If    '${ENV}' == 'QTP'    Set Overwrite Agent QTP

Set Overwrite Agent WIN
    Action Set    &AGENT#    ${_PROMPTSET_AGENT_WIN}
    Action Set    &LOGIN#    ${_PROMPTSET_LOGIN_WIN}

Set Overwrite Agent UNIX
    Action Set    &AGENT#    ${_PROMPTSET_AGENT_UNIX}
    Action Set    &LOGIN#    ${_PROMPTSET_LOGIN_UNIX}

Set Overwrite Agent QTP
    Action Set    &AGENT#    ${_PROMPTSET_AGENT_QTP}
    Action Set    &LOGIN#    ${_PROMPTSET_LOGIN_QTP}

Action Report Should Contain
    [Arguments]    ${text}
    ${report} =    Action Get Report
    Should Contain    ${report}    ${text}

Action Report Should Be Found
    ${report} =    Action Get Report
    Should Not Be Empty    ${report}

Action Report Should Not Be Found
    ${report} =    Action Get Report
    Should Be Empty    ${report}

Action Return Code Should Be
    [Arguments]    ${code}
    ${returnCode}=    Action Get Variable    Return code
    Should Be Equal    ${returnCode}    ${code}

Action Return Code Should Not Equal
    [Arguments]    ${code}
    ${returnCode}=    Action Get Variable    Return code
    Should Not Be Equal    ${returnCode}    ${code}

Action Return Status Should Be
    [Arguments]    ${status}
    ${returnStatus}=    Action Get Variable    Status
    Should Be Equal    ${returnStatus}    ${status}

Action Object Variable File Path Should Be
    [Arguments]    ${Variable_Name}    ${File_name}
    ${File_path}=    Action Get Variable    ${Variable_Name}
    Should End With    ${File_path}    ${File_name}
    Set Global Variable    ${File_path}

Action Object Variable Count Value Should Be
    [Arguments]    ${Variable_Name}
    ${Count_Value}=    Action Get Variable    ${Variable_Name}
    Should Be True    ${Count_Value} >= 0

Action Object Variable Should Be Found
    [Arguments]    ${Object_Variable}
    ${objectVariable}=    Action Get Variable    ${Object_Variable}
    Should Not Be Empty    ${objectVariable}
    Set Global Variable    ${objectVariable}

Action Object Variable Should Not Be Found
    [Arguments]    ${Object_Variable}
    ${objectVariable}=    Action Get Variable    ${Object_Variable}
    Should Be Empty    ${objectVariable}

Assert Success
    Action Report Should Be Found
    Action Return Status Should Be    ${ENDED_OK}
    Action Return Code Should Be    ${RETURN_CODE_SUCCESS}

Assert Failure
    Action Report Should Be Found
    Action Return Status Should Be    ${ENDED_NOT_OK}
    Action Return Code Should Be    ${RETURN_CODE_FAILURE}

Assert Mandatory
    Action Report Should Not Be Found

Delete File
    Action Create    ${_DELETE_FILE_ACTION}
    Action Set    &UC4RB_OPS_FILE_PATH#    ${File_path}
    Action Set    &UC4RB_OPS_STATUS#    YES
    Set Overwrite Agent
    Action Execute

Genarate String
    ${Generated_Name}    Generate Random String    8    [LOWER]
    Set Global Variable    ${Generated_Name}

Get Auth Token
    [Arguments]    ${Identity_URL}    ${Username}    ${Password}    ${Tenant_Name}
    Action Create    ${_GET_AUTH_TOKEN_ACTION}
    Action Set    &UC4RB_OPS_IDENTITY_URL#    ${Identity_URL}
    Action Set    &UC4RB_OPS_USERNAME#    ${UserName}
    Action Set    &UC4RB_OPS_PASSWORD#    ${Password}
    Action Set    &UC4RB_OPS_TENANT_NAME#    ${Tenant_Name}
    Set Overwrite Agent
    Action Execute
    ${Generated_Token_ID}=    Action Get Variable    &UC4RB_OPS_AUTH_TOKEN#
    Set Global Variable    ${Generated_Token_ID}
    ${Generated_Tenant_ID}=    Action Get Variable    &UC4RB_OPS_TENANT_ID#
    Set Global Variable    ${Generated_Tenant_ID}

Create Server
    [Arguments]    ${Compute_URL}    ${Token_ID}    ${Tenant_ID}    ${Parameter_File}
    Action Create    ${_CREATE_SERVERS_ACTION}
    Action Set    &UC4RB_OPS_COMPUTE_URL#    ${Compute_URL}
    Action Set    &UC4RB_OPS_AUTH_TOKEN#    ${Token_ID}
    Action Set    &UC4RB_OPS_TENANT_ID#    ${Tenant_ID}
    Action Set    &UC4RB_OPS_PARAMETER_FILE#    ${Parameter_File}
    Set Overwrite Agent
    Action Execute
    ${Generated_Server_ID}=    Action Get Variable    &UC4RB_OPS_SERVERLIST#[00001]
    Set Global Variable    ${Generated_Server_ID}
    Sleep    15s
